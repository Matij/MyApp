import deps.Libs
import deps.Versions

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.buildConfig = [
            'compileSdk': 29,
            'minSdk'    : 25,
            'targetSdk' : 29,
            'buildTools': "30.0.2",
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.AndroidX.Navigation.safeArgsGradlePlugin
        classpath Libs.dexcountGradlePlugin
        classpath Libs.spotlessGradlePlugin

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://dl.bintray.com/kotlin/kotlinx" }
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target "**/src/**/*.kt"
            ktlint(Versions.ktlint)
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = false

            jvmTarget = Versions.JAVA_VERSION_STR

            // Enable experimental coroutines APIs, including Flow
            freeCompilerArgs += "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"
            freeCompilerArgs += "-Xuse-experimental=kotlinx.coroutines.FlowPreview"
            // Enable kotlin.Result
            freeCompilerArgs += "-Xallow-result-return-type"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
